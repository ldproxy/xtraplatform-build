plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'version-catalog'
    id 'maven-publish'
}

def isBranch = project.hasProperty('branch')
def isMainBranch = isBranch && (project.getProperty('branch') == 'master' || project.getProperty('branch') == 'main')
def isRelease = project.findProperty('release') == 'true'
ext {
    versionSuffix = (isBranch && !isMainBranch && !isRelease ? ('-' + project.getProperty('branch')) : '') + (!isRelease ? '-SNAPSHOT' : '')
}

group = 'de.interactive_instruments'

version = '5.4.0' + versionSuffix

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

sourceSets {
    annotations {
        java {
            srcDir 'src/annotations/java'
        }
    }
}

java {
    registerFeature('annotations') {
        usingSourceSet(sourceSets.annotations)
    }
}

sourceCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation pluginLibs.bundles.all
}

gradlePlugin {
    plugins {
        layerPlugin {
            id = 'de.interactive_instruments.xtraplatform-layer'
            implementationClass = 'de.interactive_instruments.xtraplatform.LayerPlugin'
        }
        applicationPlugin {
            id = 'de.interactive_instruments.xtraplatform-application'
            implementationClass = 'de.interactive_instruments.xtraplatform.ApplicationPlugin'
        }
        modulePlugin {
            id = 'de.interactive_instruments.xtraplatform-module'
            implementationClass = 'de.interactive_instruments.xtraplatform.ModulePlugin'
        }
        docPlugin {
            id = 'de.interactive_instruments.xtraplatform-doc'
            implementationClass = 'de.interactive_instruments.xtraplatform.docs.DocPlugin'
        }
        compositePlugin {
            id = 'de.interactive_instruments.xtraplatform-composite'
            implementationClass = 'de.interactive_instruments.xtraplatform.CompositePlugin'
        }
        settingsPlugin {
            id = 'de.interactive_instruments.xtraplatform-settings'
            implementationClass = 'de.interactive_instruments.xtraplatform.SettingsPlugin'
        }
    }
}


def libz = rootProject.extensions
        .getByType(VersionCatalogsExtension).named("libs")

catalog {
    versionCatalog {
        libz.getVersionAliases().each {
            version(it, "${libz.findVersion(it).get().displayName}")
        }
        libz.getPluginAliases().each {
            plugin(it, "${libz.findPlugin(it).get().get().pluginId}").versionRef("${it}")
        }
        libz.getLibraryAliases().each {
            def versionAliases = libz.getVersionAliases().sort {a, b -> b.length() <=> a.length()}
            def versionRef = versionAliases.find {alias -> it == alias } ?: versionAliases.find {alias -> it.startsWith(alias) }
            library(it, "${libz.findLibrary(it).get().get().group}", "${libz.findLibrary(it).get().get().name}").versionRef(versionRef)
        }
        libz.getBundleAliases().each {
            def libAliases = libz.getLibraryAliases().sort {a, b -> b.length() <=> a.length()}
            def libRefs = libz.findBundle(it).get().get().collect {dep ->
                def name = dep.name.replaceAll("-", ".")
                libAliases.find {alias -> name == alias } ?: libAliases.find {alias -> name.startsWith(alias) } ?: libAliases.find {alias -> dep.group.endsWith(alias) }
            }
            bundle(it, libRefs)
        }
    }
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://dl.interactive-instruments.de/repository/maven-releases/"
            def snapshotsRepoUrl = "https://dl.interactive-instruments.de/repository/maven-snapshots/"

            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.findProperty('deployUser') ?: ''
                password project.findProperty('deployPassword') ?: ''
            }
        }
    }
    publications {
        catalog(MavenPublication) {
            artifactId = 'xtraplatform-catalog'
            from components.versionCatalog
        }
    }
}

